buildscript {
    ext {
        springBootVersion = '1.3.0.RELEASE'
        alpnBootVersion = '8.1.6.v20151105'
    }

    repositories {
        jcenter()
        maven { url "https://repo.spring.io/release" }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

plugins {
  id 'java'
  id 'application'
  id 'eclipse'
  id 'idea'
}
apply plugin: 'spring-boot'


group = 'org.test'
version = '0.0.1-SNAPSHOT'

description = "demo-http2"

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    jvmBoot { description "additional libs on jvm bootclasspath" }
}

repositories {
     jcenter()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter')
    compile('org.springframework:spring-webmvc')
    compile ('org.springframework.boot:spring-boot-starter-undertow') {
        exclude(module: 'undertow-websockets-jsr')
    }
    testCompile('org.springframework.boot:spring-boot-starter-test') {
        exclude(module: 'commons-logging')
    }
    jvmBoot "org.mortbay.jetty.alpn:alpn-boot:${alpnBootVersion}"
}

defaultTasks 'bootRun'
mainClassName = 'demo.DemoHttp2Application'
applicationDefaultJvmArgs = ["-Xbootclasspath/p:lib/alpn-boot-${alpnBootVersion}.jar"]

distributions {
    main.contents {
        from(configurations.jvmBoot) {
            into "lib"
        }
    }
}

task copyAlpnBootJar (type:Copy) {
    from (configurations.jvmBoot)
    into("lib")
}

tasks["eclipse"].dependsOn(copyAlpnBootJar)
processResources.dependsOn copyAlpnBootJar

clean {
    delete "lib/*.jar"
}
